/*
 Project <PROJECT_NAME>

 Gradle build file for JaCaMo Applications
 <DATE>
*/

defaultTasks 'run'

apply plugin: 'java'
apply plugin: 'eclipse'

version '1.0'
group   'org.jacamo'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()

  //maven { url "https://github.com/cleberjamaral/cartago/raw/maven2" }
  
  //flatDir { dirs 'lib' }
  maven { url "https://github.com/cleberjamaral/camel-artifact/raw/maven2" }
  maven { url "http://jacamo.sourceforge.net/maven2" }
  maven { url "http://jade.tilab.com/maven/" }

}

ext {
	versions = [
		camel       : '2.17.6',
	]
}

dependencies {
  compile name: 'cartago', version: '2.4-SNAPSHOT', changing: true , transitive: true, group: 'org.jacamo'
  compile name: 'jaca', version: '2.4-SNAPSHOT', changing: true , transitive: true, group: 'org.jacamo'
  
  compile group: 'org.jacamo', name: 'jacamo', version: '0.8-SNAPSHOT'
  // https://mvnrepository.com/artifact/commons-lang/commons-lang
  compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
  // https://mvnrepository.com/artifact/org.apache.camel/camel-core
  compile group: 'org.apache.camel', name: 'camel-core', version: versions.camel
  // https://mvnrepository.com/artifact/org.apache.camel/camel-telegram
  compile group: 'org.apache.camel', name: 'camel-telegram', version: '2.22.1'
  // https://mvnrepository.com/artifact/javax.xml.soap/javax.xml.soap-api
  compile group: 'javax.xml.soap', name: 'javax.xml.soap-api', version: '1.4.0'
  // https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
  compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  // https://mvnrepository.com/artifact/javax.activation/activation
  compile group: 'javax.activation', name: 'activation', version: '1.1.1'
 
  //https://github.com/cleberjamaral/camel-artifact/blob/maven2/nz/ac/otago/camelartifact/0.0.2/camelartifact-0.0.2.jar
  compile group: 'nz.ac.otago', name: 'camelartifact', version: '0.0.2'

}

sourceSets {
  main {
	  java {
		  srcDir 'src/env'
		  srcDir 'src/agt'
		  srcDir 'src/org'
		  srcDir 'src/java'
	  }
	  resources {
		  srcDir 'src/resources'
	  }
  }
}

task run (type: JavaExec, dependsOn: 'classes') {
  group       ' JaCaMo'
  description 'runs the JaCaMo application'
  doFirst {
	  mkdir 'log'
  }
  main 'jacamo.infra.JaCaMoLauncher'
  args 'auctiontelegram.jcm'
  classpath sourceSets.main.runtimeClasspath
}


task uberJar(type: Jar, dependsOn: 'classes') {
  group      ' JaCaMo'
  description 'creates a single runnable jar file with all dependencies'

  manifest {
	  attributes 'Main-Class': 'jacamo.infra.JaCaMoLauncher'
  }
  baseName = 'jacamo-auctiontelegram' // the name must start with jacamo so that jacamo...jar is found in the classpath
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  from (project.projectDir.absolutePath) {
	  include '**/*.asl'
	  include '**/*.xml'
	  include '**/*.jcm'
	  include '*.properties'
  }
  from (project.buildDir.absolutePath + '/jcm') {
	  include '**/*'
  }
  with jar

  doFirst {
	  copy {
		  from 'auctiontelegram.jcm'
		  rename 'auctiontelegram.jcm','default.jcm'
		  into project.buildDir.absolutePath + '/jcm'
	  }
  }
}

clean {
  delete 'bin'
  delete 'build'
  delete 'log'
}